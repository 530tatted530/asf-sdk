apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = '0.2.0a'

android {
  compileSdkVersion 27

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion 27
    versionCode 2
    versionName "0.2.0a"

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    consumerProguardFiles 'proguard-rules.pro'
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    warning 'InvalidPackage'
  }

  compileOptions {
    targetCompatibility 1.8
    sourceCompatibility 1.8
  }
}

dependencies {
  testImplementation lib.junit
  testImplementation lib.mockito_core

  implementation lib.support_annotations

  api lib.rxjava
  api lib.web3j
}

task sourcesJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "sources"
}

task javadoc(type: Javadoc) {
  source = android.sourceSets.main.java.srcDirs
  classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

  failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      groupId 'com.asfoundation'
      artifactId 'sdk-android'
      version project.version
      artifact(sourcesJar)
      artifact(javadocJar)
      artifact("$buildDir/outputs/aar/appcoins-sdk-release.aar")

      //The publication doesn't know about our dependencies, so we have to manually add them to the pom
      pom.withXml {
        def dependenciesNode = asNode().appendNode('dependencies')

        //Iterate over the api dependencies (we don't want the test ones), adding a <dependency> node for each
        configurations.api.allDependencies.each {
          def dependencyNode = dependenciesNode.appendNode('dependency')
          dependencyNode.appendNode('groupId', it.group)
          dependencyNode.appendNode('artifactId', it.name)
          dependencyNode.appendNode('version', it.version)
        }
      }
    }
  }
  repositories {
    maven {
      url "$buildDir/repo"
    }
  }
}

bintray {
  user = "${ASF_BINTRAY_USER}"
  key = "${ASF_BINTRAY_KEY}"
  publications = ['MyPublication']
  pkg {
    repo = 'asf'
    name = 'appcoins-sdk'
    licenses = ['GPL-3.0']
    vcsUrl = 'https://github.com/Aptoide/AppCoins-ethereumj.git'

    version {
      name = project.version
    }
  }
}

